		ORG 	4400Q
;
;
OUTR	EQU 	3227H
INP		EQU		3242H
MINCH	EQU 	300Q
MAXCH	EQU 	077Q
;
; DIV SUB
;
LDIV:	CALL	CSIGN
		CALL	ZCHK
		JNZ		DTST2
		CALL	BCHK
		JZ 		INDFC
		JMP		WZERC
DTST2:	CALL	BCHK
		JZ 		OFLWC
		MOV		E,L
		MOV		L,C
		CALL	DCLR
		MOV		L,E
		CALL	ENT1
		MOV		L,C
		CALL	DLST
		MVI		D,23
REP3:	MOV		L,E
		CALL	ENT2
		DCR 	D
		JZ 		GOON
		MOV		A,L
		MOV		L,C
		MOV		C,A
		CALL	DLST
		MOV		A,L
		MOV		E,C
		MOV		C,A
		JMP		REP3
;
GOON:	CALL	AORS
		JM 		CRIN
		MOV		A,L
		MOV		L,C
		MOV		C,A
		CALL	DLST
		MOV		C,L
		MOV		L,E
		CALL	LDCP
		RET
;
CRIN:	CALL	CFCHE
		SUB		E
		CPI		177Q
		JZ 		OFLWC
		ADI		1
		CALL	CCHK
		RET
;
; ADD SUB
;
LADD:	XRA		A
		JMP		LADS
;
; SUB SUB
;
LSUB:	MVI		A,200Q
LADS:	CALL	ACPR
		CALL	BCHK
		RZ
		CALL	CCMP
		JZ 		EQ02
		MOV		D,A
		JC		LLTB
		SUB 	E
		ANI		127
		MOV		C,A
		MOV		E,L
		MOV		L,C
		INR		L
		MOV		M,E
		MOV		L,B
		JMP		NCHK
LLTB:	MOV		A,E
		SUB 	D
		ANI		127
		MOV		D,A
NCHK:	MVI		A,24
		CMP 	D
		JNC 	SH10
		MVI		D,24
SH10:	ORA		A
		CALL 	DRST
		DCR 	D
		JNZ 	SH10
EQUL:	MOV		A,L
		CMP 	B
		JNZ		EQ02
		MOV		L,C
		INR		L
		MOV		L,M
EQ02:	CALL	LASD
		CALL	ACPR
		CPI		2
		JNZ 	NOT0
		JMP		WZER
;
NOT0:	MVI		D,1
		ANA		D
		JZ 		ADDZ
		CALL	TSTR
		JZ 		SUBZ
		MOV		A,L
		MOV		L,B
		MOV		B,A
;
SUBZ:	CALL	DSUB
		CALL	MANT
		CALL	TSTR
		JZ 		NORM
		MOV		A,L
		MOV		L,B
		MOV		B,A
		MOV		A,C
		MOV		C,B
		MOV		E,L
		MOV		B,A
		CALL	LXFR
		MOV		A,B
		MOV		B,C
		MOV		C,A
		MOV		L,E
		JMP		NORM
;
ADDZ:	CALL	CCMP
		JNC		ADD2
		CALL	BCTL
ADD2:	CALL	MANT
		CALL 	DADD
		JNC		SCCFG
		CALL	DRST
		CALL	INCR
		RET
;
MANT:	MOV		E,L
		MOV 	L,C
		MOV		A,M
		ANI		128
		MOV		L,E
		INR 	L
		INR 	L
		INR 	L
		MOV		E,A
		MOV		A,M
		ANI		127
		ADD 	E
		MOV		M,A
		DCR 	L
		DCR 	L
		DCR 	L
		RET
;
LASD:	CALL	MSFH
		CMP 	E
		JC 		ABCH
		JNZ		BBCH
		ADD 	E
		JC 		BMIN
		CALL	AORS
		JP 		L000
COM1:	CALL	DCMP
		JC 		L131
		JNZ 	L001
L002:	MVI		A,2
		RET
BMIN:	CALL	AORS
		JP 		L128
COM2:	CALL	DCMP
		JC 		L003
		JNZ 	L129
		JMP 	L002
ABCH:	CALL	AORS
		JM 		L000
		JMP		COM1
BBCH:	CALL	AORS
		JM 		L128
		JMP		COM2
L000:	XRA		A
		RET
L001:	MVI		A,1
		RET
L003:	MVI		A,3
		RET
L128:	MVI		A,128
		RET
L129:	MVI		A,129
		RET
L131:	MVI		A,131
		RET
;
LMCM:	CALL	CCMP
		RNZ
		CALL 	DCMP
		RET
;
; MUL SUB
;
LMUL:	CALL	CSIGN
		CALL 	ZCHK
		JZ 		WZERC
		CALL	BCHK
		JZ 		WZERC
		MOV		E,L
		MOV		L,C
		CALL	DCLR
		MOV		L,E
		MVI		D,24
KPG0:	CALL	DRST
		JC 		MADD
		MOV		A,L
		MOV		L,C
		MOV		C,A
INTR:	CALL 	DRST
		MOV 	A,L
		MOV 	L,C
		MOV 	C,A
		DCR 	D
		JNZ 	KPG0
		CALL	AORS
		JM 		LMCP
		MOV		E,L
		MOV		L,C
		CALL	DLST
		MOV		L,E
		CALL 	CFCHE
		ADD 	E
		CPI		200Q
		JZ 		UFLWC
		SUI		1
		CALL 	CCHK
		RET
;
MADD:	MOV		A,L
		MOV		L,C
		MOV		C,A
		CALL	DADD
		JMP		INTR
;
; NORM SUB
;
NORM:	MOV		E,L
NORM1:	CALL	GCHAR
		MOV		D,A
FXL1:	MOV		L,E
FXL2:	CALL	ZMCHK
		JZ 		WZER
REP6:	MOV		A,M
		ORA 	A
		JM 		SCHAR
		MOV		A,D
		CPI		MINCH
		JZ 		WUND
		CALL	DLST
		DCR 	D
		JMP		REP6
SCHAR:	JMP		INCR3
;
DFXL:	MOV		E,L
		INR 	L
		INR 	L
		INR 	L
		XRA		A
		MOV		M,A
		MOV		L,E
FLOAT:	MVI		D,24
		JMP		FXL2
;
ZCHK:
ZMCHK:	INR 	L
		INR 	L
		MOV		A,M
		DCR 	L
		ORA 	M
		DCR 	L
		ORA 	M
		RET
;
BCHK:	MOV		E,L
		MOV		L,B
		CALL 	ZCHK
		MOV		L,E
		RET
;
DLST:	INR 	L
		INR 	L
		MOV		A,M
		ORA		A
		RAL 
		MOV		M,A
		DCR 	L
		MOV 	A,M
		RAL 
		MOV		M,A
		DCR 	L
		MOV 	A,M
		RAL
		MOV		M,A
		RET
;
DRST:	MOV 	E,L
		MOV 	A,M
		RAR
		MOV		M,A
		INR 	L
		MOV		A,M
		RAR
		MOV		M,A
		INR 	L
		MOV 	A,M
		RAR
		MOV 	M,A
		MOV 	L,B
		RET
;
DADD:	MOV 	E,L
		MOV		L,B
		INR 	L
		INR 	L
		MOV 	A,M
		MOV 	L,E
		INR 	L
		INR 	L
		ADD 	M
		MOV 	M,A
		MOV		L,B
		INR 	L
		MOV 	A,M
		MOV		L,E
		INR 	L
		ADC 	M
		MOV 	M,A
		MOV 	L,B
		MOV		A,M
		MOV		L,E
		ADC		M
		MOV		M,A
		RET
;
DCLR:	XRA 	A
		MOV		M,A
		INR 	L
		MOV 	M,A
		INR 	L
		MOV 	M,A
		DCR 	L
		DCR 	L
		RET
;
DSUB:	MOV		E,L
		INR 	L
		INR 	L
		MOV 	A,M
		MOV 	L,B
		INR 	L
		INR 	L
		SUB 	M
		MOV 	L,E
		INR 	L
		INR 	L
		MOV 	M,A
		DCR 	L
		MOV 	A,M
		MOV		L,B
		INR 	L
		SBB		M
		MOV		L,E
		INR 	L
		MOV 	M,A
		DCR 	L
		MOV 	A,M
		MOV 	L,B
		SBB		M
		MOV 	L,E
		MOV 	M,A
		RET
;
GCHAR:	INR 	L
		INR 	L
		INR 	L
		MOV 	A,M
		ANI 	177Q
		ADI		100Q
		XRI 	100Q
		RET
;
CFCHE:	MOV		E,L
		MOV 	L,B
		CALL 	GCHAR
		MOV 	L,E
		MOV 	E,A
		CALL  	GCHAR
		DCR 	L
		DCR 	L
		DCR  	L
		MOV 	D,A
		RET
;
CCMP:	CALL	CFCHE
		MOV		D,A
		SUB 	E
		RAL
		MOV		A,D
		RET
;
; ERRORS RETURNS
;
;WFLT	MACRO VMANT,VCHAR,VFLAG,LABEL
;		MVI		D,VCHAR
;		CALL	WCHAR
;LABEL::	MVI		A,VMANT
;		CALL 	WMANT
;		MVI		A,VFLAG
;		ORA 	A
;		RET
;		ENDM
;
WUND:	;WFLT 0,100Q,377Q,UFLW1
		MVI		D,00040H
		CALL	WCHAR
UFLW1:	MVI		A,00000H
		CALL 	WMANT
		MVI		A,000FFH
		ORA 	A
		RET
WOVR:	;WFLT 377Q,77Q,177Q,OFLW1
		MVI		D,0003FH
		CALL	WCHAR
OFLW1:	MVI		A,000FFH
		CALL	WMANT
		MVI		A,0007FH
		ORA		A
		RET
WIND:	;WFLT 377Q,77Q,77Q,INDF1
		MVI		D,0003FH
		CALL 	WCHAR
INDF1:	MVI		A,000FFH
		CALL 	WMANT
		MVI		A,0003FH
		ORA		A
		RET
;
WZER:	INR 	L
		INR 	L
		INR 	L
		MVI 	M,100Q
		XRA 	A
		CALL 	WMANT
		ORA 	A
		RET
;
WMANT:	DCR 	L
		MOV 	M,A
		DCR 	L
		MOV		M,A
		DCR 	L
		MOV		M,A
		RET
;
WCHAR:	INR 	L
		INR 	L
		INR 	L
		MOV 	A,M
		ANI		200Q
		ORA 	D
		MOV 	M,A
		RET
;
INDFC:	MOV		E,L
		MOV		L,C
		CALL	WIND
		MOV		L,E
		RET
;
WZERC:	MOV		E,L
		MOV		L,C
		CALL	WZER
		MOV		L,E
		RET
;
INCR:	CALL	GCHAR
		CPI		MAXCH
		JZ 		OFLW1
		MOV		D,A
		INR 	D
		JMP 	INCR2
INCR3:	INR 	L
		INR 	L
		INR 	L
INCR2:	MVI		A,177Q
		ANA		D
		MOV		D,A
		MOV		A,M
		ANI		200Q
		ORA		D
		MOV		M,A
		DCR 	L
		DCR 	L
		DCR 	L
SCCFG:	XRA		A
		RET
;
DECR:	CALL	GCHAR
		CPI		MINCH
		JZ 		UFLW1
		MOV		D,A
		DCR 	D
		JMP 	INCR2
;
AORS:	MOV		E,L
		MOV		L,C
		MOV 	A,M
		ORA		A
		MOV		L,E
		RET
;
TSTR:	MOV		E,L
		MOV		L,C
		MVI		D,2
		MOV		A,M
		MOV		L,E
		ANA		D
		RET
;
ACPR:	MOV		E,L
		MOV		L,C
		MOV		M,A
		MOV		L,E
		RET
;
DCMP:	MOV		A,M
		MOV		E,L
		MOV		L,B
		CMP 	M
		MOV		L,E
		RNZ
		INR 	L
		MOV		A,M
		MOV		L,B
		INR 	L
		CMP 	M
		MOV		L,E
		RNZ 
		INR 	L
		INR 	L
		MOV		A,M
		MOV		L,B
		INR 	L
		INR 	L
		CMP 	M
		MOV		L,E
		RET
;
ENT2:	CALL	DLST
		JC 		OVER
ENT1:	CALL	DCMP
		JNC 	OVER
		RET
OVER:	CALL	DSUB
		MOV		E,L
		MOV		L,C
		INR 	L
		INR 	L
		MOV		A,M
		ADI 	1
		MOV 	M,A
		MOV 	L,E
		RET
;
LXFR:	MVI		D,4
REP5:	MOV		L,C
		MOV		A,M
		MOV		L,E
		MOV		M,A
		INR 	C
		INR 	E
		DCR 	D
		JNZ 	REP5
		MOV		A,E
		SUI		4
		MOV		E,A
		MOV		A,C
		SUI 	4
		MOV		C,A
		RET
;
LDCP:	CALL	CFCHE
		SUB 	E
		JMP 	CCHK
LMCP:	CALL	CFCHE
		ADD 	E
CCHK:
		CPI		100Q
		JC 		STORC
		CPI 	200Q
		JC 		OFLWC
		CPI 	300Q
		JC 		UFLWC
STORC:	MOV		E,L
		MOV 	L,C
		MOV 	D,A
		CALL	INCR3
		MOV 	L,E
		RET
;
OFLWC:	MOV		E,L
		MOV 	L,C
		CALL 	WOVR
		MOV		L,E
		RET
UFLWC:	MOV		E,L
		MOV		L,C
		CALL	WUND
		MOV		L,E
		RET
CSIGN:	CALL	MSFH
		XRA 	D
		CALL 	CSTR
		RET
CSTR:	MOV		E,L
		MOV		L,C
		INR 	L
		INR 	L
		INR 	L
		MOV 	M,A
		MOV 	L,E
		RET
;
MSFH:	MOV		E,L
		MOV		L,B
		INR 	L
		INR 	L
		INR 	L
		MOV		A,M
		ANI		128
		MOV		L,E
		MOV		E,A
		INR 	L
		INR 	L
		INR 	L
		MOV		A,M
		ANI		128
		DCR 	L
		DCR 	L
		DCR 	L
		RET
;
BCTL:	MOV		E,L
		MOV 	L,B
		INR 	L
		INR 	L
		INR 	L
		MOV		A,M
		MOV 	L,E
		INR 	L
		INR 	L
		INR 	L
		MOV 	M,A
		MOV		L,E
		RET
;
DSQRT:	MOV		A,L
		MOV		L,C
		MVI		M,0
		INR 	L
		MOV 	M,A
		INR 	L
		MOV 	M,B
		INR 	L
		MOV 	C,L
		MOV 	L,A
		MOV 	A,H
		CALL	COPY
		CALL 	GCHR
		MOV 	B,A
		ANI 	200Q
		JNZ 	ERSQ
		MOV 	A,B
		ANI		100Q
		MOV 	A,B
		JZ 		EPOS
		RAR
		ANI 	177Q
		ORI 	100Q
		MOV 	M,A
		JMP 	AGN4
EPOS:	RAR
		ANI 	177Q
		MOV 	M,A
AGN4:	MOV 	L,C
		MOV 	A,C
		ADI 	4
		MOV 	C,A
		MOV 	A,H
		CALL 	COPY
		MOV 	A,C
		SUI 	4
		MOV 	L,A
		MOV 	B,C
		ADI 	10Q
		MOV 	C,A
		CALL 	LMUL
		MOV		A,C
		SUI 	10Q
		MOV 	C,A
		SUI 	2
		MOV 	L,A
		MOV		L,M
		MOV 	A,H
		CALL 	COPY
		MOV 	A,C
		ADI 	10Q
		MOV 	L,A
		MOV 	B,C
		ADI 	4
		MOV 	C,A
		CALL 	LADD
		MOV 	A,L
		SUI 	4
		MOV 	B,A
		SUI 	4
		MOV 	C,A
		CALL	LDIV
		CALL 	GCHR
		SUI 	1
		ANI 	177Q
		MOV 	M,A
		MOV 	A,C
		SUI 	3
		MOV 	L,A
		MOV 	B,M
		INR 	B
		MOV 	M,B
		MOV 	A,B
		CPI 	5
		JNZ 	AGN4
		MOV 	L,C
ALDN:	DCR 	L
		MOV 	C,M
		INR 	L
		MOV 	A,H
		CALL 	COPY
		RET
ERSQ:	MOV		L,C
		CALL 	WZER
		JMP 	ALDN
;
CVRT:	CALL	ZCHK
		JNZ		NNZRO
		INR 	C
		INR 	C
		MOV 	L,C
		CALL	WZER
		CALL	SIGN
;
		INR 	L
		INR 	L
		INR 	L
		INR 	L
		XRA 	A
		MOV 	M,A
		JMP		MDSKP
NNZRO:	MOV		D,M
		INR 	L
		MOV 	B,M
		INR 	L
		MOV 	E,M
		INR 	L
		MOV 	A,M
		INR 	C
		INR 	C
		MOV 	L,C
		MOV 	M,D
		INR 	L
		MOV 	M,B
		INR 	L
		MOV 	M,E
		INR 	L
		MOV 	B,A
		ANI		177Q
		MOV		M,A
		CPI		100Q
		JZ 		NZRO
		SUI 	1
		ANI 	100Q
NZRO:	RLC
		INR 	L
		MOV 	M,A
		MOV 	A,B
		CALL 	SIGN
		MVI		L,(TEN5 AND 377Q)
		CALL 	COPT
TST8:	CALL	GCHR
		MOV 	B,A
		ANI	 	100Q
		MOV 	A,B
		JZ 		GOTV
		MVI		A,200Q
		SUB 	B
GOTV:	CPI		22Q
		JM 		TRY1
		CALL 	MORD
		ADI		5
		MOV 	M,A
		JMP		TST8
TRY1:	MVI 	L,(TEN AND 377Q)
		CALL 	COPT
TST1:	CALL	GCHR
		CPI 	1
		JP 		OK1
MDGN:	CALL	MORD
		ADI 	1
		MOV 	M,A
		JMP 	TST1
OK1:	CPI 	7
		JP 		MDGN
MDSKP:	MOV 	L,C
		DCR 	L
		DCR 	L
		MVI 	M,5
		MOV 	E,A
		CALL	LSFT
		CPI 	12Q
		JP 		TWOD
		CALL 	DIGO
POPD:	CALL 	MULTT
INPOP:	CALL 	DIGO
		JNZ 	POPD
		MVI 	A,305Q
		CALL	OUTR
		CALL 	GETEX
		MOV		B,A
		CALL 	SIGN
		MOV		A,B
		ANI		77Q
		CALL 	CTWO
DIGO:	ADI 	260Q
		CALL	OUTR
		MOV 	L,C
		DCR 	L
		DCR 	L
		MOV 	A,M
		CPI 	5
		MVI 	A,256Q
		CZ 		OUTR
		MOV		D,M
		DCR 	D
		MOV 	M,D
		RET
MULTT:	MVI		E,1
		CALL	LSFT
		MOV		L,C
		DCR 	L
		MOV		A,C
		ADI		11Q
		MOV		C,A
		MOV		A,H
		CALL	COPY
		MOV 	A,C
		SUI		11Q
		MOV 	C,A
		MVI 	E,2
		MOV 	L,C
		DCR 	L
		CALL 	TLP2
		MOV 	L,C
		MOV 	A,C
		ADI 	12Q
		MOV 	B,A
		CALL 	DADD
		DCR 	L
		MOV 	A,M
		MOV 	L,B
		DCR 	L
		MOV 	A,M
		MOV 	L,B
		DCR 	L
		ADC 	M
		RET
LSFT:	MOV		L,C
		DCR 	L
		XRA 	A
TLOOP:	MOV 	M,A
TLP2:	DCR 	E
		RM
		INR 	L
		INR 	L
		INR 	L
		MOV		A,M
		RAL
		MOV 	M,A
		DCR 	L
		MOV		A,M
		RAL
		MOV 	M,A
		DCR 	L
		MOV		A,M
		RAL
		MOV 	M,A
		DCR 	L
		MOV		M,A
		RAL
		JMP 	TLOOP
SIGN:	ANI 	200Q
		MVI	 	A,240Q
		JZ 		PLSV
		MVI 	A,255Q
PLSV:	CALL	OUTR
		RET
GCHR:	MOV 	L,C
GETA:	INR 	L
		INR 	L
		INR 	L
		MOV 	A,M
		RET
MORD:	CALL 	GETEX
		MOV 	E,A
		MOV 	B,L
		INR 	B
		MOV 	L,C
		MOV 	A,C
		ADI 	11Q
		MOV 	C,A
		MOV 	A,E
		ANI 	200Q
		JZ 		DIVIT
		CALL 	LMUL
FINUP:	MOV 	A,C
		MOV 	C,L
		MOV 	L,A
		MOV 	A,H
		CALL 	COPY
GETEX:	MOV 	L,C
		INR 	L
		JMP 	GETA
DIVIT:	CALL	DIVIT
		JMP 	FINUP
TWOD:	CALL	CTWO
		MOV		B,A
		CALL 	GETEX
		MOV 	E,A
		ANI		200Q
		JZ 		ADD1
		DCR 	E
FINIT:	MOV 	M,E
		MOV 	A,B
		JMP		INPOP
ADD1:	INR 	E
		JMP 	FINIT
CTWO:	MVI		E,377Q
LOOP:	INR 	E
		SUI 	12Q
		JP 		LOOP
		ADI 	12Q
		MOV 	B,A
		MOV 	A,E
		CALL	DIGO
		MOV 	A,B
		RET
COPT:	MOV 	A,C
		ADI 	5
		MOV 	C,A
		MVI 	A,(TEN5/256)
		CALL	COPY
		MOV 	A,C
		SUI	 	5
		MOV 	C,A
		RET
COPY:	MOV 	B,H
		MOV 	H,A
		MOV 	A,M
		INR 	L
		MOV		D,M
		INR 	L
		MOV		E,M
		INR 	L
		MOV 	L,M
		MOV 	H,B
		MOV 	B,L
		MOV 	L,C
		MOV 	M,A
		INR 	L
		MOV 	A,M
		MOV 	M,D
		INR 	L
		MOV 	M,E
		INR 	L
		MOV 	M,B
		RET
;
TEN5:	DB 	303Q,120Q,0Q,21Q
TEN:	DB 	240Q,0Q,0Q,4Q
;
ERR:	MVI		A,277Q
		CALL	OUTR
		MVI 	A,240Q
		CALL 	OUTR
		JMP 	PRMT
;
INPUT:	MOV 	B,L
		MOV 	A,C
		ADI 	17Q
		MOV 	L,A
		MOV 	M,B
		INR 	C
		INR 	C
PRMT:	MVI 	A,272Q
		CALL 	OUTR
		CALL 	ZROIT
		INR 	L
		MOV 	M,A
		CALL 	GNUM
		CPI 	376Q
		JZ 		DECPT
TSTEX:	CPI 	25Q
		JZ 		INEXP
		CPI 	360Q
		JNZ 	ERR
		CALL 	FLTSGN
SCALE:	CALL 	GETEX
		ANI 	177Q
		MOV 	E,A
		ANI 	100Q
		RLC
		ORA 	A
		MOV 	B,A
		MOV 	A,E
		JZ 		APLS
		MVI 	A,200Q
		SUB 	E
APLS:	ADD 	B
		MOV 	M,A
		MVI 	L,(TEN5 AND 377Q)
		CALL 	COPT
		CALL 	GETEX
INT5:	ANI 	77Q
		CPI 	5Q
		JM 		TRYTN
		CALL 	MORD
		SUI 	5Q
		MOV 	M,A
		JMP 	INT5
TRYTN:	MVI 	L,(TEN AND 377Q)
		CALL 	COPT
		CALL 	GETEX
INT1:	ANI 	77Q
		ORA 	A
		JZ 		SAVEN
		CALL 	MORD
		SUI 	1Q
		MOV 	M,A
		JMP 	INT1
DECPT:	MOV 	L,C
		DCR 	L
		DCR 	L
		MVI 	M,0
		CALL 	EP1
		MOV 	E,A
		MOV 	L,C
		DCR 	L
		DCR 	L
		MOV 	B,M
		CALL	GETEX
		MOV 	M,B
		MOV 	A,E
		JMP 	TSTEX
INEXP:	CALL 	FLTSGN
		CALL 	SAVEN
		CALL 	ZROIT
		CALL	GNUM
		CPI 	360Q
		JNZ 	ERR
		MOV 	L,C
		INR 	L
		INR 	L
		MOV 	A,M
		ANI 	37Q
		MOV 	B,A
		INR 	L
		MOV 	A,M
		ORA 	A
		MOV 	A,B
		JM 		USEIT
		MVI 	A,0Q
		SUB 	B
USEIT:	INR 	L
		ADD 	M
		MOV 	M,A
		MOV 	A,C
		ADI 	15Q
		MOV 	L,A
		MOV 	L,M
		MOV 	A,H
		CALL 	COPY
		JMP 	SCALE
GNUM:	CALL 	INP
		CPI 	240Q
		JZ 		GNUM
		CPI 	255Q
		JNZ 	TRYP
		MOV 	L,C
		INR 	L
		INR 	L
		INR 	L
		MVI 	M,200Q
		JMP 	GNUM
TRYP:	CPI 	253Q
		JZ 		GNUM
TSTN:	SUI 	260Q
		RM
		CPI		12Q
		RP
		MOV		E,A
		CALL	GETN
		MOV 	M,E
		CALL 	MULTT
		ORA 	A
		RNZ
		CALL	GETN
		MOV 	L,C
		INR 	L
		INR 	L
		ADD 	M
		MOV 	M,A
		DCR 	L
		MOV 	A,M
		ACI 	0Q
		MOV 	M,A
		DCR 	L
		MOV 	A,M
		ACI 	0Q
		MOV 	M,A
		RC
		DCR 	L
		DCR 	L
		MOV 	B,M
		INR 	B
		MOV 	M,B
EP1:	CALL 	INP
		JMP 	TSTN
FLTSGN:	MOV 	L,C
		JMP 	FLOAT
SAVEN:	MOV 	A,C
		ADI 	15Q
		MOV 	L,A
		MOV 	E,M
		MOV 	L,E
		INR 	L
		MOV 	M,C
		MOV 	L,C
		MOV 	C,E
		MOV 	A,H
		CALL 	COPY
		MOV 	C,A
		RET
GETN:	MOV 	A,C
		ADI 	16Q
		MOV 	L,A
		MOV 	A,M
		RET
ZROIT:	MOV 	L,C
		XRA 	A
		MOV 	M,A
		INR 	L
		MOV 	M,A
		INR 	L
		MOV 	M,A
		INR 	L
		MOV 	M,A
		RET
;
FPTBL:
        JMP    	NORM
        JMP    	FLOAT
        JMP    	WZER
        JMP    	LADD
        JMP    	LMUL
        JMP    	LDIV
        JMP    	LSUB
        JMP    	DFXL
        JMP    	LMCM
        JMP    	COPY
        JMP     CVRT
        JMP    	INPUT

		END